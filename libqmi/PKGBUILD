# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libqmi
pkgver=1.20.0
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=(x86_64)
url="http://www.freedesktop.org/wiki/Software/libqmi/"
license=(GPL2)
depends=(libmbim libgudev)
makedepends=(gtk-doc python git help2man)
_commit=a7fd5363cff7af6137065065ce405adf7bcbfc35  # tags/1.20.0
source=("git+https://anongit.freedesktop.org/git/libqmi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
