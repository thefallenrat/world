# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Yejun Yang <yejunx AT gmail DOT com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=libsrtp
pkgver=2.1.0
pkgrel=1
epoch=1
pkgdesc="Library for SRTP (Secure Realtime Transport Protocol)"
url="https://github.com/cisco/libsrtp"
arch=(x86_64)
license=(BSD)
depends=(openssl)
makedepends=(git)
checkdepends=(libpcap procps-ng)
options=(staticlibs)
_commit=93aba3e767502343c255fad8962dbf0ff29c4ca6  # tags/v2.1.0
source=("git+https://github.com/cisco/libsrtp#commit=$_commit")
sha256sums=('SKIP')

# Broken: There are tags like v1.5.4 but also "moving" tags like v1 and v1.5
#pkgver() {
#  cd $pkgname
#  git describe --tags | sed 's/^v//;s/-/+/g'
#}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-openssl
  make all
  make shared_library  # Keep separate to link test apps against static lib
}

check() {
  cd $pkgname
  make runtest
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
