# Maintainer: artoo <artoo@cromnix.org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=desktop-presets
pkgname=(
        'artix-icons'
        'artix-backgrounds'
        'artix-i3-presets'
        'artix-lxqt-presets'
        'artix-cinnamon-presets'
)
pkgver=0.6.1
pkgrel=1
arch=('any')
url="https://github.com/artix-linux/desktop-presets"
license=('GPL')
makedepends=('git')
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('9ac7d9e5d112702eecde79b9ec960dee9a41e6c4a465c9ea32da26c80b3a2ec4')

_inst_dir(){
    cd ${pkgbase}-${pkgver}
    local profile="$1"

    install -d ${pkgdir}/etc
    cp -r $profile/skel ${pkgdir}/etc

    if [[ -d $profile/schemas ]];then
        install -d ${pkgdir}/usr/share/glib-2.0/schemas
        cp -r $profile/schemas ${pkgdir}/usr/share/glib-2.0/schemas
    fi

    if [[ -d $profile/scripts ]];then
        install -d ${pkgdir}/usr/bin
        cp -r $profile/scripts ${pkgdir}/usr/bin
    fi
}

package_artix-backgrounds(){
    pkgdesc='Artix backgrounds'

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_backgrounds
}

package_artix-icons(){
    pkgdesc='Artix icons'

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_icons
}

package_artix-lxqt-presets() {
    pkgdesc='Artix lxqt presets'
    depends=('breeze-icons'
            'breeze-gtk'
            'artix-backgrounds'
            'artix-icons')
    provides=('desktop-presets')

    _inst_dir 'lxqt'
}

package_artix-cinnamon-presets() {
    pkgdesc='Artix cinnamon presets'
    depends=('artix-backgrounds'
            'artix-icons')
    provides=('desktop-presets')

    _inst_dir 'cinnamon'
}

package_artix-i3-presets() {
    pkgdesc='Artix i3 presets'
    depends=('breeze-icons'
            'breeze-gtk'
            'artix-backgrounds'
            'artix-icons')
    provides=('desktop-presets')

    _inst_dir 'i3'
}
