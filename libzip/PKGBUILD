# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libzip
pkgver=1.4.0
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives"
url="http://www.nih.at/libzip/index.html"
license=('BSD')
arch=('x86_64')
depends=('zlib' 'bzip2')
makedepends=('cmake')
source=("http://www.nih.at/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e508aba025f5f94b267d5120fc33761bcd98440ebe49dbfe2ed3df3afeacc7b1')

build() {
  cd "${pkgname}-${pkgver}"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${pkgname}-${pkgver}"/build

  make DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
