# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=whois
pkgver=5.2.20
pkgrel=1
pkgdesc='Intelligent WHOIS client'
url='https://github.com/rfc1036/whois'
license=('GPL')
arch=('x86_64')
depends=('libidn')
makedepends=('perl')
source=("http://ftp.debian.org/debian/pool/main/w/whois/${pkgname}_${pkgver}.tar.xz")
sha256sums=('6848ab671750ab3782fe4ab2a47910fe4e25aa93894e4d0f3f67b5fcee06c009')

backup=('etc/whois.conf')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make \
		CONFIG_FILE=/etc/whois.conf \
		CFLAGS="$CFLAGS $CPPFLAGS" \
		HAVE_ICONV=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make prefix=/usr BASEDIR="${pkgdir}" install-whois
	install -D -m644 whois.conf "${pkgdir}/etc/whois.conf"
}
