# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-wayland
_qtver=5.10.0
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite' 'wayland') # namcap note: wayland is needed for nvidia-libgl users
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qtbug-65553.patch::"https://github.com/qt/qtwayland/commit/bf09c7a1.patch"
        qtwayland-key-compose.patch::"https://github.com/qt/qtwayland/commit/57c4af2b.patch")
sha256sums=('5657f9656c95f83880f92e7697a1c13c9739bf4d0bfd867e711fff7c84004b93'
            'e2437aa2bca824396cf1df6c4a4f311b59ff1a823886a5d00fb687cb9553fba7'
            '401debccda18236869d601883e1ea9c0d0a541ea12656e43ef9e7d53b835dd92')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtbug-65553.patch # Fix frequent crashes in Plasma Wayland
  patch -p1 -i ../qtwayland-key-compose.patch # Backport key composition support
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
