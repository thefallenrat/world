# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Pajaro

pkgname=liblo
pkgver=0.29
pkgrel=1
epoch=1
pkgdesc="A lightweight OSC (Open Sound Control) implementation"
arch=('i686' 'x86_64')
url="http://liblo.sourceforge.net/"
license=('GPL')
depends=('glibc')
makedepends=('doxygen')
source=("https://github.com/radarsat1/${pkgname}/releases/download/${pkgver}/$pkgname-$pkgver.tar.gz")
sha512sums=('11e20725a049c55c02ef8cd106be5802507f3df95422d8dad10009bdd1237af7517e3261c7336c3005e21ae8fd1e663badef46e4bae006c7af70520bdb336c7c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # package docs, by request; the package size is less than 1MB
  # reasonable to include even if it increases from a few KBs to 100s of KBs -- schiv
  mkdir -p ${pkgdir}/usr/share/{man/man3,doc/${pkgname}/{html,latex},${pkgname}/examples}

  # delete broken man pages
  rm -f doc/man/man3/_*.3

  # move all doc
  install -Dm 644 doc/html/* ${pkgdir}/usr/share/doc/${pkgname}/html
  install -Dm 644 doc/latex/* ${pkgdir}/usr/share/doc/${pkgname}/latex
  install -Dm 644 doc/man/man3/*.3 ${pkgdir}/usr/share/man/man3
  install -Dm 644 examples/*.{cpp,c} ${pkgdir}/usr/share/${pkgname}/examples
}

# vim:set ts=2 sw=2 et:
