# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=runit-artix
pkgver=20180108
pkgrel=2
pkgdesc='Runit initscripts for Artix'
provides=('runit-init')
url='https://github.com/artix-linux/runit-artix/'
license=('custom:CC0')
arch=('i686' 'x86_64')
depends=('procps-ng' 'awk' 'sh' 'eudev')
install='runit-artix.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/artix-linux/runit-artix/archive/${pkgver}.tar.gz")
backup=('etc/runit/rc.conf'
        'etc/runit/rc.local'
        'etc/runit/rc.shutdown')
sha256sums=('b764d647ddddd4bbf263c068f74d401eb1b564e1bf8d6e1929f41ec797f97637')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" PREFIX="/usr" install

	# License
	mkdir -p "${pkgdir}"/usr/share/licenses/runit-artix
	sed -n '/To the extent/,/0/p' README.md > "${pkgdir}"/usr/share/licenses/runit-artix/LICENSE
}
