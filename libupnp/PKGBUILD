# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org> 
# Contributor: kastor <kastor@fobos.org.ar>

pkgname=libupnp
pkgver=1.6.24
pkgrel=2
pkgdesc="Portable Open Source UPnP Development Kit"
arch=('x86_64')
url="http://pupnp.sourceforge.net/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig')
source=("http://downloads.sourceforge.net/sourceforge/pupnp/${pkgname}-${pkgver}.tar.bz2"
        "segfault_http_makemessage.patch")
md5sums=('c5f4a3b674741d85ef29258841ccd540'
         'b5ab030107cadfb307dc1b809b7a5fe3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/segfault_http_makemessage.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
