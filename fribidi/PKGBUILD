# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=fribidi
pkgver=0.19.7
pkgrel=2
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
arch=('x86_64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
depends=('glib2')
makedepends=(git)
_commit=1a6935cd8cd7d907fb3c5f3bcae174bee727c83d  # tags/0.19.7^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}


prepare() {
  cd $pkgname
  git cherry-pick -n 0efbaa9052320a951823a6e776b30a580e3a2b4e
  ./bootstrap
}

build() {
  cd "${srcdir}/$pkgname"

  ./configure --prefix=/usr

  make -j1
}

package() {
  cd "${srcdir}/$pkgname"

  make -j1 DESTDIR="${pkgdir}" install
}
